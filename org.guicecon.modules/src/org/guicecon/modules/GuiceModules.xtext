grammar org.guicecon.modules.GuiceModules 
	with org.eclipse.xtext.xbase.Xbase
 
generate guiceModules "http://www.guicecon.org/modules/GuiceModules"
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types
  
Module :
	'package' packageName=QualifiedName
	imports+=Import*
	'module' name=ID ('mixin' mixins+=[Module|QualifiedName] (',' mixins+=[Module|QualifiedName])*)? '{'
		bindings+=Binding*
	'}'
;

Binding:
	override?='override'? 'bind' from=Key 
	('to' to=Key
	 |'toInstance' toInstance=XExpression)?
;

Key:
	annotation=AnnotationRef? type=JvmTypeReference
;

AnnotationRef:
	'@' annotation=[types::JvmAnnotationType|QualifiedName]
;

Import:
	'import' importedNamespace=QualifiedNameWithWildCard
;

QualifiedNameWithWildCard:
	QualifiedName '.*'?
;


/* 
 * key( @Named('foo') TypeProvider<String> )
 */